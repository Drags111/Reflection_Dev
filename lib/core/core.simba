(*
Core
====

Contains routines necessary for function that couldn't be filed in
other places.

*)

(*
R_LatestRevision
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_LatestRevision(WhatKind: string): Integer;

Gets Hooks.simba information from pyroryan's server.

.. note::

  by Naike, Harry

*)
function R_LatestRevision(WhatKind: string): Integer;
var
  S: String;
  I: Integer;
begin
  case whatKind of
    'clientversion':
    begin
      S := GetPage('http://reflection.harry.lu/Hooks.simba');
      I := Pos('ClientVersion', S);
      Delete(S, 1, I+15);
      S := Copy(S, 1, Pos(';', S)-1);
      Result := StrToIntDef(S, -1);
      if Result = -1 then
        WriteLn('[Reflection] Failed to get Hook Revision!');
    end;

    'hookrevision':
    begin
      S := GetPage('http://reflection.harry.lu/Hooks.simba');
      I := Pos('HookRev', S);
      Delete(S, 1, I+9);
      S := Copy(S, 1, Pos(';', S)-1);
      Result := StrToIntDef(S, -1);
      if Result = -1 then
        WriteLn('[Reflection] Failed to get Hook Revision!');
    end;
    else
      WriteLn('[Reflection] r_GetCurrentGit - invalid option!');
  end;
end;

(*
R_UpdateHooks
~~~~~~~~~~~~~

.. code-block:: pascal

    Procedure R_UpdateHooks(Terminate: boolean);

Tries to update to new hooks!

.. note::

  by Naike, Harry

*)
Procedure R_UpdateHooks;
var
  MyFile, ClientRev, RevHook: Integer;
begin
  ClientRev := R_LatestRevision('clientversion');
  RevHook := R_LatestRevision('hookrevision');
  If (ClientRev <= ClientVersion) and (RevHook <= HookRev) then
  begin
    if(Client.LoginIndex() = -1) then
    begin
      WriteLn('[Reflection] No new hooks have been uploaded yet; please be patient!');
      if not R_ContinueIfOutdated then
        TerminateScript;
    end;
  end else
  begin
    WriteLn('[Reflection] You''re using outdated hooks (version ' + ToStr(ClientVersion) + '.' + ToStr(HookRev) + '); updating you to version' + ToStr(ClientRev) + '.' + ToStr(RevHook) + '!');
    WriteLn('[Reflection] If you manually fixed your hooks, please make sure hookrevision values are greater than or equal to the online hooks to prevent overwriting.');
    MyFile := RewriteFile(AppPath + 'includes/Reflection_Dev/lib/core/hooks.simba', False);
    WriteFileString(MyFile, GetPage('http://reflection.harry.lu/Hooks.simba'));
    CloseFile(MyFile);
    WriteLn('[Reflection] Updated you to ' + ToStr(ClientRev) + '.' + ToStr(RevHook) + '; please restart your script!');
    TerminateScript;
  end;
end;

(*
R_FreeObjects
~~~~~~~~~~~~~

.. code-block:: pascal

    procedure R_FreeObjects(Objects: TIntegerArray);

Properly frees multiple objects using a try-catch statement.

.. note::

  by Drags111

Example:
  R_FreeObjects([ThisNode, ThatNode, TheOtherNode]);

*)
procedure R_FreeObjects(Objects: TIntegerArray);
var
  I: Integer;
begin
  for I := 0 to High(Objects) do
  begin
    try
      SmartFreeObject(Objects[i]);
    except
    end;
  end;
end;

(*
R_FreeObject
~~~~~~~~~~~~

.. code-block:: pascal

    procedure R_FreeObject(Obj: Integer);

Properly frees an object using a try-catch statement.

.. note::

  by Drags111

*)
procedure R_FreeObject(Obj: Integer);
begin
  try
    SmartFreeObject(Obj);
  except
  end;
end;

(*
R_TrimHTML
~~~~~~~~~~

.. code-block:: pascal

    function R_TrimHTML(S : string): string;

Removes the HTML junk from a string.

.. note::

  by lordsaturn, pyroryan

*)
function R_TrimHTML(S : string): string;
var
  P, P2, PLength: integer;
  Temp: string;
begin
  P := Pos('<', S);
  P2 := Pos('>', S);
  Result := S;
  PLength := P2 - P + 1;
  while (P <> 0) and (P2 <> 0) do
  begin
    Delete(Result, P, PLength);
    P := Pos('<', Result);
    Temp := Result;
    Delete(Temp, 1, P - 1);
    PLength := Pos('>', Temp);
  end;
end;

(*
R_GetJavaString
~~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_GetJavaString(javaString: integer; maxLength: integer): string;

Returns a string from a javaString with a specified  maxLength. Do not let it
overflow, bad things will happen.

.. note::

  by BenLand100

*)
function R_GetJavaString(javaString: integer; maxLength: integer): string;
begin
  SetLength(result, maxLength);
  SetLength(result, SmartStringFromString(javaString, result));
end;

(*
R_GetArraySize
~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_GetArraySize(arrayObject : Integer) : Integer;

Retrives the size of the array object through smart.

.. note::

  by pyroryan

*)
function R_GetArraySize(arrayObject : Integer) : Integer;
begin
  Result := SmartGetFieldArraySize(arrayObject, '', 1);
end;

(*
R_GetSettingArray
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_GetSettingArray: TIntegerArray;

Retrives the Settings array from the client.

.. note::

  by Drags111

*)
function R_GetSettingArray: TIntegerArray;
var
  SettingArray, Data, DL, i: Integer;
begin
  try
    SettingArray := Client.Settings();
    Data := SmartGetFieldObject(SettingArray, hook_settingarray_GetData);

    DL := SmartGetFieldArraySize(Data, '', 1);
    SetLength(Result, DL);
    for i := 0 to DL-1 do
      Result[i] := SmartGetFieldArrayInt(data, '', i);
  finally
    SmartFreeObject(Data);
    SmartFreeOBject(SettingArray);
  end;
end;

(*
R_GetSetting
~~~~~~~~~~~~

.. code-block:: pascal

    function R_GetSetting(Setting: Integer): Integer;

Gets the setting value at the specified index.

.. note::

  by Drags111

*)
function R_GetSetting(Setting: Integer): Integer;
var
  SA: TIntegerArray;
begin
  SA := R_GetSettingArray;
  if(Setting < Length(SA))then
    Result := SA[Setting]
  else
  begin
    R_Debug('Incorrect setting index passed to GetSetting', 'R_GetSetting');
    Result := -1;
  end;
end;

(*
function R_LoggedIn: Boolean;
~~~~~~~~~~~~

.. code-block:: pascal

    function R_LoggedIn: Boolean;

Returns true if you're logged in.

.. note::

  by Method, drags111, Zyt3x

*)
function R_LoggedIn: Boolean;
var
  LoginIndex: Integer;
begin
  LoginIndex := Client.LoginIndex();
  Result := (LoginIndex = 10) or (LoginIndex = 11);
end;

(*
R_GetMyPos
~~~~~~~~~~

.. code-block:: pascal

    function R_GetMyPos: TTile;

Returns your current tile.

.. note::

  by Benland100

*)
function R_GetMyPos: TTile;
var
  me: Integer;
begin
  me := Client.MyPlayer();
  Result.x := Client.BaseX() + SmartGetFieldInt(me, hook_animable_PixelX) / 512;
  Result.y := Client.BaseY() + SmartGetFieldInt(me, hook_animable_PixelY) / 512;
  R_FreeObject(me);
end;

(*
R_GetMyLocPos
~~~~~~~~~~~~~

.. code-block:: pascal

    function R_GetMyLocPos: TTile;

Returns your local position.

.. note::

  by Benland100

*)
function R_GetMyLocPos: TTile;
var
  me: Integer;
begin
  me := Client.MyPlayer();
  Result.x := (SmartGetFieldInt(me, hook_animable_PixelX) - Client.BaseX()) / 512;
  Result.y := (SmartGetFieldInt(me, hook_animable_PixelY) - Client.BaseY()) / 512;
  R_FreeObject(me);
end;
