(*
R_GetClickContinueInterface
~~~~~~~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_GetClickContinueInterface: TInterfaceChild;

Searches for a "Click here to Continue" in the interfaces, and returns the
TInterfaceChild that contains it.

.. note::

  by Drags111

*)
function R_GetClickContinueInterface: TInterfaceChild;
var
  I, K: Integer;
  ValidInterfaces, ValidChildren: TIntegerArray;
  ChildText: String;
  Child: TInterfaceChild;
begin
  ValidInterfaces := R_GetValidInterfaces;

	for I := 0 to High(ValidInterfaces) do
  begin
		if(ValidInterfaces[I] = 137)then
			Continue;

    ValidChildren := R_GetValidChildren(ValidInterfaces[I]);

		for K := 0 to High(ValidChildren) do
    begin
			ChildText := R_GetInterfaceText(ValidInterfaces[I], K);
			if (ChildText = '')then
				Continue;
			if (Pos('Click here to continue', ChildText) > 0)then
      begin
				Result := R_GetInterfaceChild(ValidInterfaces[I], K);
        Exit;
      end;
		end;
	end;
end;

(*
R_GetNPCChatName
~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_GetNPCChatName: String;

Searches for an NPC name at the top of the chat, if it exists, and returns it.

.. note::

  by Drags111

*)
function R_GetNPCChatName: String;
var
  Cont: TInterfaceChild;
  InterfaceChildren: TIntegerArray;
  I, Index: Integer;
  Text: String;
begin
  Cont := R_GetClickContinueInterface;
  if(Cont.ID <= 0)then
    Exit;

  Index := Cont.ParentID shr 16;
  InterfaceChildren := R_GetValidChildren(Index);

  for I := 0 to High(InterfaceChildren) do
  begin
    Text := R_GetInterfaceText(Index, I);
    if((Text <> '') and (Length(Text) > 0))then
    begin
      Result := Text;
      Exit;
    end;
  end;
end;

(*
R_GetNPCChatMessages
~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_GetNPCChatMessages: TStringArray;

Searches for each line of an NPC chat message, and returns the lines in a
TStringArray.

.. note::

  by Drags111

*)
function R_GetNPCChatMessages: TStringArray;
var
  Cont: TInterfaceChild;
  Children: TIntegerArray;
  Index, I, C: Integer;
  IsName: Boolean;
  Text: String;
begin
  Cont := R_GetClickContinueInterface;
  if(Cont.ID <= 0)then
    Exit;

  Index := Cont.ParentID shr 16;
  Children := R_GetValidChildren(Index);
  SetLength(Result, Length(Children));
  C := 0;

  if(Length(Children) < 1)then
    Exit;

  IsName := True;

  for I := 0 to High(Children) do
  begin
    Text := R_GetInterfaceText(Index, I);
    if(Text <> '') and (Length(Text) > 0)then
    begin
      if(IsName)then
      begin
        IsName := False;
        Continue;
      end else
        if(Text = 'Click here to continue')then
          Break;
      Result[C] := R_TrimHTML(Text);
      Inc(C);
    end;
  end;
  SetLength(Result, C);
end;

(*
R_GetNPCChatMessage
~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_GetNPCChatMessage: String;

Returns the NPC Chat message, if one exists, in one string.

.. note::

  by Drags111

*)
function R_GetNPCChatMessage: String;
var
  Messages: TStringArray;
  I: Integer;
begin
  Result := '';
  Messages := R_GetNPCChatMessages;
  for I := 0 to High(Messages)do
    Result := Result + Messages[I] + ' ';
  Result := Trim(Result);
end;

(*
R_ClickToContinue
~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_ClickToContinue: Boolean;

If the click to continue interface exists, it will click it.

.. note::

  by Drags111

*)
function R_ClickToContinue: Boolean;
var
  Cont: TInterfaceChild;
  T, Color: Integer;
  TPA: TPointArray;
begin
  Result := False;
  Cont := R_GetClickContinueInterface;

  if(Cont.ID <= 0)then
    Exit;

  if(Cont.IsHidden)then
    Exit;

  R_ClickInterface(Cont, 1);
  MarkTime(T);
  while(R_ValidInterface(Cont.ParentID shr 16))do
  begin
    if(TimeFromMark(T) > 5000)then
      Exit;
    Wait(100+Random(100));
  end;

  Result := True;
end;

(*
R_ClickContinueExists
~~~~~~~~~~~~~~~~~~~~~

.. code-block:: pascal

    function R_ClickContinueExists: Boolean;

Returns true if the "Click here to continue" text exists.

.. note::

  by Drags111

*)
function R_ClickContinueExists: Boolean;
var
  Cont: TInterfaceChild;
begin
  Result := False;
  Cont := R_GetClickContinueInterface;
  Result := Cont.ID > 0;
end;
